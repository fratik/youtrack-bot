plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
}

group 'pl.fratik'
def versionObj = new Version(major: 1, minor: 0, revision: 0, dopisek: "")
version = "$versionObj".toString()

sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'pl.fratik.youtrackbot.Main'

def jdaVersion = '4.1.1_101'

build.dependsOn shadowJar

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url 'https://jitpack.io' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    compile 'net.dv8tion:JDA:' + jdaVersion
    compile 'com.intellij:annotations:+@jar'
    compile 'com.google.code.gson:gson:2.8.6'
    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
    compile 'com.squareup.okhttp3:okhttp:3.12.10'
    compile 'org.json:json:20190722'
    compile 'org.slf4j:slf4j-api:1.7.29'
    compile 'com.google.guava:guava:23.0'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.tuxdude.logback.extensions:logback-colorizer:1.0.1'
    compile 'com.github.queer:async-postgres-jsonb-orm:e348e109f8'
    compile 'io.undertow:undertow-core:2.0.28.Final'
    compile 'io.undertow:undertow-servlet:2.0.28.Final'
    compile 'io.undertow:undertow-websockets-jsr:2.0.28.Final'
    compile 'commons-io:commons-io:2.6'
//    testCompile group: 'junit', name: 'junit', version: '4.12'
}

shadowJar {
    manifest {
        attributes 'Implementation-Title': 'YouTrack Bot', 'Implementation-Version': version
        attributes 'Main-Class': 'pl.fratik.youtrackbot.Main'
    }
    mergeServiceFiles()
    mergeGroovyExtensionModules()
}

compileJava {
    options.compilerArgs << '-parameters'
    options.encoding = 'UTF-8'
}

artifacts {
    archives shadowJar
}

compileJava.options.encoding = 'UTF-8'

/*
 * Copyright (C) 2020 YouTrack Bot Contributors
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 *
 * YouTrack is a product released by JetBrains s.r.o.
 * and this project is not affiliated with them.
 */

class Version {
    String major, minor, revision, dopisek

    static String getBuild() {
        System.getenv("BUILD_NUMBER") ?: System.getProperty("BUILD_NUMBER") ?:
                System.getenv("GIT_COMMIT")?.substring(0, 7) ?: System.getProperty("GIT_COMMIT")?.substring(0, 7) ?:"DEV"
    }

    String toString() {
        "$major.$minor.${revision}_$build$dopisek"
    }
}
